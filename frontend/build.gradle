buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath "com.github.node-gradle:gradle-node-plugin:3.4.0"
    }
}

apply plugin: 'base'
apply plugin: "com.github.node-gradle.node"

npm_run_build {
    inputs.files fileTree('public')
    inputs.files fileTree('src')

    inputs.file 'package.json'
    inputs.file 'package-lock.json'

    outputs.dir 'build'
}

node {
    version = '16.16.0'

    download = true
}

task packageNpmApp(type: Zip) {
    dependsOn npm_run_build
    baseName 'npm-app'
    extension 'jar'
    destinationDir file("${projectDir}/build_npm")
    from('build') {
        into 'static'
    }
}

configurations {
    npmResources
}

configurations.default.extendsFrom(configurations.npmResources)

artifacts {
    npmResources(packageNpmApp.archiveFile) {
        builtBy packageNpmApp
        type 'jar'
    }
}

assemble.dependsOn packageNpmApp

clean {
    delete packageNpmApp.archiveFile
}